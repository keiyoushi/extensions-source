import com.android.build.gradle.tasks.PackageAndroidArtifact
import org.jetbrains.kotlin.gradle.dsl.JvmTarget

apply plugin: 'com.android.application'
apply plugin: 'kotlinx-serialization'

assert !ext.has("pkgNameSuffix")
assert !ext.has("libVersion")

assert extName.chars().max().asInt < 0x180 : "Extension name should be romanized"

Project theme = ext.has("themePkg") ? project(":lib-multisrc:$themePkg") : null
if (theme != null) evaluationDependsOn(theme.path)

android {
    compileSdk AndroidConfig.compileSdk

    namespace "eu.kanade.tachiyomi.extension"

    defaultConfig {
        minSdk AndroidConfig.minSdk
        targetSdk AndroidConfig.targetSdk
        applicationIdSuffix project.parent.name + "." + project.name
        versionCode theme == null ? extVersionCode : theme.baseVersionCode + overrideVersionCode
        versionName "1.4.$versionCode"
        base {
            archivesName = "tachiyomi-$applicationIdSuffix-v$versionName"
        }
        assert extClass.startsWith(".")
        manifestPlaceholders = [
                appName : "Tachiyomi: $extName",
                extClass: extClass,
                nsfw    : project.ext.find("isNsfw") ? 1 : 0,
        ]
        String baseUrl = project.ext.find("baseUrl") ?: ""
        if (theme != null && !baseUrl.isEmpty()) {
            def split = baseUrl.split("://")
            assert split.length == 2
            def path = split[1].split("/")
            manifestPlaceholders += [
                    SOURCEHOST  : path[0],
                    SOURCESCHEME: split[0],
            ]
        }
    }

    signingConfigs {
        release {
            storeFile rootProject.file("signingkey.jks")
            storePassword System.getenv("KEY_STORE_PASSWORD")
            keyAlias System.getenv("ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            vcsInfo.include false
        }
    }

    dependenciesInfo {
        includeInApk = false
    }

    buildFeatures {
        buildConfig true
    }

    packaging {
        resources.excludes.add("kotlin-tooling-metadata.json")
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

//    kotlinter {
//        experimentalRules = true
//        disabledRules = [
//            "experimental:argument-list-wrapping", // Doesn't play well with Android Studio
//            "experimental:comment-wrapping",
//        ]
//    }
}

androidComponents {
    onVariants(selector().all(), { variant ->
        variant.sources.manifests.addStaticManifestFile("AndroidManifest.xml")
        variant.sources.java.addStaticSourceDirectory("src")
        variant.sources.res.addStaticSourceDirectory("res")
        variant.sources.assets.addStaticSourceDirectory("assets")
    })
}

kotlin {
    compilerOptions {
        freeCompilerArgs.add("-opt-in=kotlinx.serialization.ExperimentalSerializationApi")
        jvmTarget = JvmTarget.JVM_1_8
    }
}

dependencies {
    if (theme != null) implementation(theme) // Overrides core launcher icons
    implementation(project(":core"))
    compileOnly(libs.bundles.common)
}

tasks.register("copyManifestFile", Copy) {
    from 'AndroidManifest.xml'
    rename { 'tempAndroidManifest.xml' }
    into layout.buildDirectory
}

tasks.register("writeManifestFile") {
    dependsOn(copyManifestFile)
    doLast {
        File tempFile = android.sourceSets.getByName('main').manifest.srcFile
        if (!tempFile.exists()) {
            tempFile.write('<?xml version="1.0" encoding="utf-8"?>\n<manifest />\n')
        }
    }
}

afterEvaluate {
    tasks.withType(PackageAndroidArtifact).configureEach {
        // need to be in afterEvaluate to overwrite default value
        createdBy = ""
        // https://stackoverflow.com/a/77745844
        doFirst { appMetadata.asFile.getOrNull()?.write('') }
    }
}

//preBuild.dependsOn(writeManifestFile, lintKotlin)
//if (System.getenv("CI") != "true") {
//    lintKotlin.dependsOn(formatKotlin)
//}
